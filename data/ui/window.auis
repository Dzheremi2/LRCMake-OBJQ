define {
  ui.icons -> ico
}

TEMPLATE:ChronographWindow <- ui.ApplicationWindow:
  ! actions: ["min", "max", "close"]
  ! resizeable: true
  ! min-width: 360
  ! min-height: 100

  ui.navigation.Navigation:
    ! id: "navigation"

    ui.navigation.SidebarNavigation:
      ! id: "sidebar_nav"
      ! sidebar: sidebar
      ! sidebar-side: LEFT
      ! sidebar-toggled: bind toggle_sidebar_button.visible inverted

      Column:
        ui.headers.HeaderBar:
          [start]
          ui.buttons.Button:
            ! id: "toggle_sidebar_button"
            ! icon: ico.SIDEBAR_LEFT
            ! tooltip: R.Strings.toggle_sidebar_button_tooltip
            ! visible: true
            $ clicked: toggle_sidebar_button_clicked
          ui.buttons.MenuButton:
            ! icon: ico.OPEN_DIR
            ! tooltip: R.Strings.open_dir_button_tooltip
            ! menu: source_menu
          ui.buttons.ToggleButton:
            ! id: "toggle_search_button"
            ! icon: ico.SEARCH_LOUPE
            ! tooltip: R.Strings.toggle_search_button_tooltip
            ! toggled: false
            ! visible: false

          [center]
          ui.headers.TitleWidget:
            ! text: R.Strings.app_name
          
          [end]
          ui.buttons.Button:
            ! id: "reparse_dir_button"
            ! icon: ico.RELOAD_CIRCLE_2_ARROWS
            ! tooltip: R.Strings.reparse_dir_button_tooltip
          ui.buttons.Button:
            ! id: "pin_directory_button"
            ! icon: ico.PIN
            ! tooltip: R.Strings.pin_directory_button_tooltip
          ui.buttons.MenuButton:
            ! id: "app_menu_button"
            ! icon: ico.APP_MENU
            ! tooltip: R.Strings.app_menu_button_tooltip
            ! menu: app_menu

        ui.headers.SearchBar:
          ! id: "search_bar"
          ! max-width: 500
          ! placeholder-text: R.Strings.search_bar_placeholder
          ! collapsed: bind toggle_search_button.toggled bidirectional

        ui.containers.ScrolledWindow:
          ! scrolling-direction: VERTICAL

          ui.containers.ContentBox:
            ! id: "library"
            ! same-sized: true
            ! valign: START
            ! halign: CENTER
            ! scrolling-direction: VERTICAL
            ! column-spacing: 12
            ! row-spacing: 12
            ! margin: 12

menu:
  ! id: "source_menu"
  section:
    item:
      ! name: R.Strings.directory
      ! action: "win.open_dir"
    item:
      ! name: R.Strings.files
      ! action: "win.open_files"